import MIL.Common
import Mathlib.Data.Real.Basic

namespace C03S06

def ConvergesTo (s : ℕ → ℝ) (a : ℝ) :=
  ∀ ε > 0, ∃ N, ∀ n ≥ N, |s n - a| < ε

example : (fun x y : ℝ ↦ (x + y) ^ 2) = fun x y : ℝ ↦ x ^ 2 + 2 * x * y + y ^ 2 := by
  ext
  ring

example (a b : ℝ) : |a| = |a - b + b| := by
  congr
  ring

example {a : ℝ} (h : 1 < a) : a < a * a := by
  convert (mul_lt_mul_right _).2 h
  · rw [one_mul]
  exact lt_trans zero_lt_one h

theorem convergesTo_const (a : ℝ) : ConvergesTo (fun _ : ℕ ↦ a) a := by
  intro ε εpos
  use 0
  intro n nge
  rw [sub_self, abs_zero]
  apply εpos

theorem convergesTo_add {s t : ℕ → ℝ} {a b : ℝ}
      (cs : ConvergesTo s a) (ct : ConvergesTo t b) :
    ConvergesTo (fun n ↦ s n + t n) (a + b) := by
  intro ε εpos
  dsimp -- this line is not needed but cleans up the goal a bit.
  have ε2pos : 0 < ε / 2 := by linarith
  rcases cs (ε / 2) ε2pos with ⟨Ns, hs⟩
  rcases ct (ε / 2) ε2pos with ⟨Nt, ht⟩
  use max Ns Nt
  intro n nge
  have ngeNs : n ≥ Ns := le_trans (le_max_left _ _) nge
  have ngeNt : n ≥ Nt := le_trans (le_max_right _ _) nge
  apply lt_of_le_of_lt (b := |s n - a| + |t n - b|)
  · have : s n + t n - (a + b) = s n - a + (t n - b) := by ring
    rw [this]
    apply abs_add
  · convert add_lt_add (hs n ngeNs) (ht n ngeNt)
    norm_num

theorem convergesTo_mul_const {s : ℕ → ℝ} {a : ℝ} (c : ℝ) (cs : ConvergesTo s a) :
    ConvergesTo (fun n ↦ c * s n) (c * a) := by
  by_cases h : c = 0
  · convert convergesTo_const 0
    · rw [h]
      ring
    rw [h]
    ring
  · intro ε εpos
    have acpos : 0 < |c| := abs_pos.mpr h
    have εcpos : 0 < ε / |c| := div_pos εpos acpos
    dsimp
    rcases cs (ε / |c|) εcpos with ⟨N, hN⟩
    use N
    intro n ngeN
    calc
      |c * s n - c * a| = |c * (s n - a)| := by noncomm_ring
      _ = |c| * |s n - a| := by apply abs_mul
      _ < |c| * (ε / |c|) := (mul_lt_mul_left acpos).mpr (hN n ngeN)
      _ = ε := by field_simp

theorem exists_abs_le_of_convergesTo {s : ℕ → ℝ} {a : ℝ} (cs : ConvergesTo s a) :
    ∃ N b, ∀ n, N ≤ n → |s n| < b := by
  rcases cs 1 zero_lt_one with ⟨N, h⟩
  use N, |a| + 1
  intro n ngeN
  calc
    |s n| = |a + (s n - a)| := by ring_nf
    _ ≤ |a| + |s n - a| := by apply abs_add
    _ < |a| + 1 := by apply add_lt_add_left (h n ngeN)

theorem aux {s t : ℕ → ℝ} {a : ℝ} (cs : ConvergesTo s a) (ct : ConvergesTo t 0) :
    ConvergesTo (fun n ↦ s n * t n) 0 := by
  intro ε εpos
  dsimp
  rcases exists_abs_le_of_convergesTo cs with ⟨N₀, B, h₀⟩
  have Bpos : 0 < B := lt_of_le_of_lt (abs_nonneg _) (h₀ N₀ (le_refl _))
  have pos₀ : ε / B > 0 := div_pos εpos Bpos
  rcases ct _ pos₀ with ⟨N₁, h₁⟩
  let N := max N₀ N₁
  use N
  intro n ngeN
  have ngeN₀ : n ≥ N₀ := by trans N; apply ngeN; apply le_max_left
  have ngeN₁ : n ≥ N₁ := by trans N; apply ngeN; apply le_max_right
  calc
    |s n * t n - 0| = |s n * (t n - 0)| := by ring_nf
    _ = |s n| * |t n - 0| := by apply abs_mul
    _ ≤ B * |t n - 0| := by
      apply mul_le_mul_of_nonneg_right
      · apply le_of_lt
        exact h₀ n ngeN₀
      · apply abs_nonneg
    _ < B * (ε / B) := (mul_lt_mul_left Bpos).mpr (h₁ n ngeN₁)
    _ = ε := by field_simp

theorem convergesTo_mul {s t : ℕ → ℝ} {a b : ℝ}
      (cs : ConvergesTo s a) (ct : ConvergesTo t b) :
    ConvergesTo (fun n ↦ s n * t n) (a * b) := by
  have h₁ : ConvergesTo (fun n ↦ s n * (t n + -b)) 0 := by
    apply aux cs
    convert convergesTo_add ct (convergesTo_const (-b))
    ring
  have := convergesTo_add h₁ (convergesTo_mul_const b cs)
  convert convergesTo_add h₁ (convergesTo_mul_const b cs) using 1
  · ext; ring
  ring

theorem convergesTo_unique {s : ℕ → ℝ} {a b : ℝ}
      (sa : ConvergesTo s a) (sb : ConvergesTo s b) :
    a = b := by
  by_contra abne
  have : |a - b| > 0 := by
    apply abs_pos.mpr
    apply sub_ne_zero.mpr
    exact abne
  let ε := |a - b| / 2
  have εpos : ε > 0 := by
    change |a - b| / 2 > 0
    linarith
  rcases sa ε εpos with ⟨Na, hNa⟩
  rcases sb ε εpos with ⟨Nb, hNb⟩
  let N := max Na Nb
  have absa : |s N - a| < ε := by
    apply hNa N
    exact le_max_left Na Nb
  have absb : |s N - b| < ε := by
    apply hNb N
    exact le_max_right Na Nb
  have : |a - b| < |a - b| :=
    calc
      |a - b| = |s N - b - (s N - a)| := by ring_nf
      _ ≤ |s N - b| + |s N - a| := abs_sub _ _
      _ < ε + ε := add_lt_add absb absa
      _ = |a - b| := by norm_num [ε]
  exact lt_irrefl _ this

section
variable {α : Type*} [LinearOrder α]

def ConvergesTo' (s : α → ℝ) (a : ℝ) :=
  ∀ ε > 0, ∃ N, ∀ n ≥ N, |s n - a| < ε

end
